angular.module 'app', ['ui.router']

angular.module('app').config ($stateProvider, $urlRouterProvider, $httpProvider, $locationProvider) ->

  $urlRouterProvider.otherwise '/'
  $locationProvider.html5Mode true

  $stateProvider
    .state 'home',
      url: '/'

      template: """
        <p>{{currentUser}}</p>
        <p><a ui-sref="logout">logout</a></p>
      """

      controller: ($scope, currentUser, $http, auth) ->
        $scope.currentUser = currentUser

      resolve:
        currentUser: ($q, $http, auth) ->
          deferred = $q.defer()

          if auth.token()

            $http.get('/api/v1').then (res) ->
              userPath = res.data?.account?.href

              if userPath?
                $http.get(userPath)
                  .then (res)-> res.data.user
                  .then deferred.resolve
              else
                auth.redirectToLogin()
          else
            auth.redirectToLogin()

          deferred.promise

    .state 'authCallback',
      url: '/auth/callback'
      controller: (auth, $location, $http, $state) ->
        auth.token $location.hash().match(/access_token=([\w-]+)/)[1]
        $state.transitionTo 'home'

    .state 'logout',
      url: '/auth/logout'
      controller: (auth) ->
        auth.logout()


  $httpProvider.interceptors.push ($window, $q, auth) ->
    responseError: (rejection) ->
      auth.redirectToLogin() if rejection.status is 401
      $q.reject rejection

    request: ($config) ->
      $config.headers['Authorization'] = "Bearer #{auth.token()}" if auth.token()
      $config

angular.module('app').factory 'auth', ($window) ->
  OAUTH_ID = '<%= Rails.application.config.oauth_id %>'
  OAUTH_CB_URL = '<%= Rails.application.config.oauth_cb_url %>'

  buildAuthUrl = () ->
    "/oauth/authorize?response_type=token&client_id=#{OAUTH_ID}&redirect_uri=#{OAUTH_CB_URL}"

  redirectToLogin: () ->
    $window.location.href = buildAuthUrl()

  logout: () ->
    delete localStorage['access_token']
    $window.location.href = '/logout'

  token: (token=null) ->
    localStorage.setItem('access_token', token) if token?

    localStorage['access_token']
